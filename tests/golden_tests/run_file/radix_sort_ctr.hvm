data Map = Free | Used | (Both a b)
data Arr = Null | (Leaf x) | (Node a b)

(Swap s a b) = switch s {
  0: (Both a b)
  _: (Both b a)
}

// Sort : Arr -> Arr
(Sort t) = (ToArr 0 (ToMap t))

// ToMap : Arr -> Map
(ToMap Null)       = Free
(ToMap (Leaf a))   = (Radix a)
(ToMap (Node a b)) = (Merge (ToMap a) (ToMap b))

// ToArr : U60 -> Map -> Arr
(ToArr x Free) = Null
(ToArr x Used) = (Leaf x)
(ToArr x (Both a b)) =
  let a = (ToArr (+ (* x 2) 0) a)
  let b = (ToArr (+ (* x 2) 1) b)
  (Node a b)

// Merge : Map -> Map -> Map
(Merge Free       Free)       = Free
(Merge Free       Used)       = Used
(Merge Used       Free)       = Used
(Merge Used       Used)       = Used
(Merge Free       (Both c d)) = (Both c d)
(Merge (Both a b) Free)       = (Both a b)
(Merge (Both a b) (Both c d)) = (Both (Merge a c) (Merge b d))
(Merge (Both a b) Used) = *
(Merge Used (Both a b)) = *

// Radix : U60 -> Map
(Radix n) =
  let r = Used
  let r = (Swap (& n 1) r Free)
  let r = (Swap (& n 2) r Free)
  let r = (Swap (& n 4) r Free)
  let r = (Swap (& n 8) r Free)
  let r = (Swap (& n 16) r Free)
  (Radix2 n r)

(Radix2 n r) =
  let r = (Swap (& n 32) r Free)
  let r = (Swap (& n 64) r Free)
  let r = (Swap (& n 128) r Free)
  let r = (Swap (& n 256) r Free)
  let r = (Swap (& n 512) r Free)
  (Radix3 n r)

(Radix3 n r) =
  let r = (Swap (& n 1024) r Free)
  let r = (Swap (& n 2048) r Free)
  let r = (Swap (& n 4096) r Free)
  let r = (Swap (& n 8192) r Free)
  let r = (Swap (& n 16384) r Free)
  (Radix4 n r)

(Radix4 n r) =
  let r = (Swap (& n 32768) r Free)
  let r = (Swap (& n 65536) r Free)
  let r = (Swap (& n 131072) r Free)
  let r = (Swap (& n 262144) r Free)
  let r = (Swap (& n 524288) r Free)
  (Radix5 n r)

(Radix5 n r) =
  let r = (Swap (& n 1048576) r Free)
  let r = (Swap (& n 2097152) r Free)
  let r = (Swap (& n 4194304) r Free)
  let r = (Swap (& n 8388608) r Free)
  r


// Reverse : Arr -> Arr
(Reverse Null)       = Null
(Reverse (Leaf a))   = (Leaf a)
(Reverse (Node a b)) = (Node (Reverse b) (Reverse a))

// Sum : Arr -> U60
(Sum Null)       = 0
(Sum (Leaf x))   = x
(Sum (Node a b)) = (+ (Sum a) (Sum b))

// Gen : U60 -> Arr
(Gen n) = (Gen.go n 0)
  (Gen.go n x) = switch n {
    0: (Leaf x)
    _:
      let a = (* x 2)
      let b = (| (* x 2) 1)
      (Node (Gen.go n-1 a) (Gen.go n-1 b))
  }

Main = (Sum (Sort (Reverse (Gen 4))))
//Main = (Sort 0)
